plugins {
    id 'java'
    id 'application'
    id 'com.diffplug.spotless' version '6.15.0'
    id 'org.beryx.jlink' version '2.26.0'
}

group 'eggcrack'
version '1.0'

java {
    sourceCompatibility = '17'
    targetCompatibility = '17'
}

run {
    standardInput = System.in
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs += ['-Xlint:deprecation', '-Xlint:unchecked']
}

application {
    mainClass = 'edu.eggcrack.Main'
    mainModule = 'edu.eggcrack'
}

spotless {
    java {
        googleJavaFormat()
    }
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    imageName = 'eggcrack'
    jpackage {
        vendor = 'orion'
        icon = 'icon/eggcrack.ico'
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
            imageOptions += ['--win-console']
        }
    }
}

repositories {
    mavenCentral()
}
